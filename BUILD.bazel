load(
    "//bazel:windows_dll_library.bzl",
    "windows_dll_library",
)

# choose which library to use depending on the OS
alias(
  visibility = ["//visibility:public"],
  name = "opentracing",
  actual = select({
    "@bazel_tools//src/conditions:windows": ":opentracing_windows",
    "//conditions:default": ":opentracing_unix",
  }),
)

windows_dll_library(
  name="opentracing_windows",
  srcs = glob(["src/**/*.cpp"], exclude = ["src/dynamic_load_unsupported.cpp", "src/dynamic_load_unix.cpp"]),
  hdrs = glob(["include/opentracing/**/*.h"]) + [
      ":include/opentracing/config.h",
      ":include/opentracing/version.h",
  ],
  # so that source files can write opentracing/... instead of include/opentracing/...
  includes=["include"],
  deps = [
    "//3rd_party:expected",
    "//3rd_party:variant",
  ],
  # sets the OPENTRACING_EXPORTS precompiler variable while building DLL
  # so that header "import symbols" tags become "export symbols" tags
  copts=["/DOPENTRACING_EXPORTS"],
)

cc_library(
    name = "opentracing_unix",
    srcs = glob(["src/**/*.cpp"], exclude=["src/dynamic_load_unsupported.cpp", "src/dynamic_load_windows.cpp"]),
    hdrs = glob(["include/opentracing/**/*.h"]) + [
        ":include/opentracing/config.h",
        ":include/opentracing/version.h",
    ],
    strip_include_prefix = "include",
    deps = [
      "//3rd_party:expected",
      "//3rd_party:variant",
    ],
    linkopts = [
      "-ldl",
    ],
)

genrule(
    name = "generate_version_h",
    srcs = glob([
        "*",
        "cmake/*",
        "src/**/*.cpp",
    ]),
    outs = [
      "include/opentracing/config.h",
      "include/opentracing/version.h"
    ],
    cmd = """
    TEMP_DIR=$$(mktemp -d)
    CONFIG_H_OUT=$${PWD}/$(location :include/opentracing/config.h)
    VERSION_H_OUT=$${PWD}/$(location :include/opentracing/version.h)
    OPENTRACING_ROOT=$$(dirname $${PWD}/$(location :CMakeLists.txt))
    cd $$TEMP_DIR
    cmake -DBUILD_TESTING=OFF -DBUILD_MOCKTRACER=OFF -L $$OPENTRACING_ROOT
    mv include/opentracing/config.h $$CONFIG_H_OUT
    mv include/opentracing/version.h $$VERSION_H_OUT
    rm -rf $$TEMP_DIR
    """,
)
